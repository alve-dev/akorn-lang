program        → declaration* EOF ;

declaration    → varDecl
               | statement ;

varDecl        → ("var" | "let") type IDENTIFIER
                 ( "=" expression )? ";" ;

type           → IDENTIFIER ;

statement      → ifStmt
               | block
               | exprStmt ;

exprStmt       → expression ";" ;

ifStmt         → "if" "(" expression ")" block
                 ( "elif" "(" expression ")" block )*
                 ( "else" block )? ;

block          → "{" declaration* "}" ;

expression     → assignment ;

assignment     → IDENTIFIER "=" assignment
               | logical_or ;

logical_or     → logical_and ( "or" logical_and )* ;

logical_and    → equality ( "and" equality )* ;

unary          → "not" unary
               | primary ;

equality       → comparison ( ("==" | "!=") comparison )* ;

comparison     → term ( (">" | ">=" | "<" | "<=") term )* ;

term           → factor ( ("+" | "-") factor )* ;

factor         → power ( ("*" | "/" | "%") power )* ;

power          → unary ( "**" power )? ;

primary        → NUMBER
               | STRING
               | "true"
               | "false"
               | IDENTIFIER
               | "write" "(" arguments? ")"
               | "(" expression ")" ;

arguments      → expression ( "," expression )* ;

